# Виведення "тайлової" графіки

Для виводу піксельної графіки можна користуватись декількома методами:
- перевизначити символи шрифту та виводити у двох режимах:
	- одноколірний (з прозорим кольором фону) розміром до 8×9 пікселів (попіксельне позиціонування); 
	- 16 колірний (без прозорого кольору) розміром 2×9 пікселів (тільки парні x-координати);  
- спрайти (15 кольорів + 1 прозорий) розміром до 11×21 пікселів із збереженням фону (попіксельне позиціонування, але займають багато місця в пам'яті);
- 16 колірні тайли без прозорого кольору майже довільного розміру (парної ширини і тільки парні x-координати);  

Для останнього варіанту я зробив такий метод де тайли зберігаються у зовнішньому файлі у вигляді дампу пам'яті 16-колірного режиму. Перед використанням вони завантажуються у ділянку пам'яті, а потім відносно швидко виводяться простим копіюванням звідти у відеосторінку.

Ось приклад процедури для виводу тайлів 8×8 пікселів:

```
procedure DrawTile8(ti,tx,ty:integer);
var tilerow,saddr:integer;
begin
  saddr:=addr(tile8)+32*ti;
  for tilerow:=0 to 7 do
    POKE((#C000+((ty+tilerow)*64+tx)),peek(saddr+4*tilerow,array[1..4] OF CHAR));
end;
```

Кожен тайл 8×8 пікселів займає 32 байти, а кожна лінія тайлу - 4 байти.
- **ti**: (tile index) номер тайлу починаючи з 0;
- **tx**: (tile x) координата для виводу;
- **ty**: (tile y) координата для виводу;
- **saddr**: (source address) адреса розміщення тайлів;

Завантажуємо файл з тайлами по адресі масиву tile8:
```
tin('TILE8.BIN',addr(tile8));  
```

Виводимо на екран конкретний тайл у потрібних координатах:
```
DrawTile8(tindex,dx,dy);
```

Аналогічно, перед виводом тайлу можна зберегти потрібну область фону у змінну чи пам'ять розміром 32 байти, щоб пізніше можна було відтворити фон.

# "Конвертація" звуків з Pico-8

Чесно кажучи, конвертацію це важко назвати - скоріше, створення звуків дещо схожих на оригінал. 

Наведу сторінки з журналу **Pico-8 Zine №1** де розписаний інтерфейс трекеру:
![](pics/pico8-musictracker.png)

Довжина звучання кожної ноти залежить від швидкості звуку (**SPD**):  
**1**: 8.31 мс  
**2**: 16.59 мс  
...  
і т.д.

Мінімальна довжина звучання ноти у Ентерпрайза становить 20 мс. Тому можна звуки зробити або повільнішими, щоб програвалась кожна нота, або для зберігання загальної довжини звуку можна використати наближений метод. Я користувався наступним. На скріншот з трекеру (де у вигляді графіку показана висота тону) накладав сітки різної роздільності. Для звуків 2-ї швидкості використовував сітку з 26 горизонтальними клітинками, а для 1-ї - з 13 клітинками. Потім прикидував яка мінімальна кількість фаз мені потрібна (крім зміни висоти звуку треба додатково зважати на зміну гучності)

![](pics/sound-spd1.png) ![](pics/sound-spd2.png)

Після чого кожна фаза огинаючої записується у тестову бейсік програму типу такої:

```
1 CLOSE #103
5 SET SOUND BUFFER 60
6 OPEN #103:"sound:"
7 set key click off

10 ENVELOPE NUMBER 10;2,63,63,1;6,0,0,1;9,0,0,1
20 ENVELOPE NUMBER 1;0,63,63,1;5,0,0,4;3,0,0,1
30 ENVELOPE NUMBER 2;0,63,63,1;6,0,0,1
40 ENVELOPE NUMBER 3;-4,63,63,1;-2,0,0,1;9,0,0,2;26,0,0,2
50 ENVELOPE NUMBER 4;10,63,63,1;-6,0,0,1;9,0,0,1;-13,-63,-63,1;16,63,63,1;-5,0,0,1;4,0,0,2;13,0,0,1;-28,-63,-63,1
60 ENVELOPE NUMBER 5;0,63,63,1;RELEASE;-20,-33,-33,40
70 ENVELOPE NUMBER 6;6,63,63,2;0,-63,-63,1;-1,0,0,4;2,63,63,2;0,-63,-63,1;1,0,0,5;3,63,63,2;0,-63,-63,1;1,0,0,5;4,63,63,2;0,-63,-63,1
80 ENVELOPE NUMBER 7;4,63,63,3;-7,-63,-63,1;-11,0,0,4;5,63,63,2;0,0,0,3;-15,0,0,4;-3,-63,-63,3
90 ENVELOPE NUMBER 8;0,63,63,6;0,0,0,6;0,-63,-63,8

93 DO
94   DO
95     A$=INKEY$
96   LOOP UNTIL A$<>""
97   SELECT CASE A$
98   CASE "@"
100     SOUND PITCH 8,LEFT 255,RIGHT 255,DURATION 3,ENVELOPE 10
105   CASE "1"
110     SOUND PITCH 7,LEFT 255,RIGHT 255,DURATION 6,ENVELOPE 1
115   CASE "2"
130     SOUND PITCH 12,LEFT 255,RIGHT 255,DURATION 2,ENVELOPE 2
135   CASE "3"
140     SOUND PITCH 16,LEFT 255,RIGHT 255,DURATION 6,ENVELOPE 3
145   CASE "4"
150     SOUND PITCH 5,LEFT 255,RIGHT 255,DURATION 18,ENVELOPE 4
155   CASE "5"
160     SOUND PITCH 50,LEFT 255,RIGHT 255,DURATION 30,ENVELOPE 5,STYLE 48
165   CASE "6"
170     SOUND PITCH 10,LEFT 255,RIGHT 255,DURATION 26,ENVELOPE 6
175   CASE "7"
180     SOUND PITCH 34,LEFT 255,RIGHT 255,DURATION 20,ENVELOPE 7
185   CASE "8"
190     SOUND PITCH 5,LEFT 20,RIGHT 20,DURATION 20,ENVELOPE 8,SOURCE 3
191   CASE "9"
195     SOUND PITCH 38,LEFT 114,RIGHT 114,DURATION 2 !9
200   END SELECT
210 LOOP
```

І коли готові звучання влаштовують, можна їх перенести у код програми, або огинаючі зберегти у окремий файл у вигляді esc-послідовностей. А потім завантаживши при старті програми використовувати їх стандартним шляхом.

Зберегти огинаючі у файл можна наступною програмою на бейсіку:
```
5 OPEN #1:"FILENAME.ENV" ACCESS OUTPUT

10 ENVELOPE #1:NUMBER 10;2,63,63,1;6,0,0,1;9,0,0,1
20 ENVELOPE #1:NUMBER 1;0,63,63,1;5,0,0,4;3,0,0,1
30 ENVELOPE #1:NUMBER 2;0,63,63,1;6,0,0,1
40 ENVELOPE #1:NUMBER 3;-4,63,63,1;-2,0,0,1;9,0,0,2;26,0,0,2
50 ENVELOPE #1:NUMBER 4;10,63,63,1;-6,0,0,1;9,0,0,1;-13,-63,-63,1;16,63,63,1;-5,0,0,1;4,0,0,2;13,0,0,1;-28,-63,-63,1
60 ENVELOPE #1:NUMBER 5;0,63,63,1;RELEASE;-20,-33,-33,40
70 ENVELOPE #1:NUMBER 6;6,63,63,2;0,-63,-63,1;-1,0,0,4;2,63,63,2;0,-63,-63,1;1,0,0,5;3,63,63,2;0,-63,-63,1;1,0,0,5;4,63,63,2;0,-63,-63,1
80 ENVELOPE #1:NUMBER 7;4,63,63,3;-7,-63,-63,1;-11,0,0,4;5,63,63,2;0,0,0,3;-15,0,0,4;-3,-63,-63,3
90 ENVELOPE #1:NUMBER 8;0,63,63,6;0,0,0,6;0,-63,-63,8

100 CLOSE #1
```


Таблиця орієнтовної відповідності нот до висоти звуку

| нота  | позначення | Pitch<br>(октава 0) | Pitch<br>(октава 1) | Pitch<br>(октава 2) | Pitch<br>(октава 3) |
|:-----:|:----------:|:-------------------:|:-------------------:|:-------------------:|:-------------------:|
|  ДО   |   **C**    |          7          |         19          |         31          |         43          |
|  ДО#  |   **C#**   |          8          |         20          |         32          |         44          |
|  РЕ   |   **D**    |          9          |         21          |         33          |         45          |
|  РЕ#  |   **D#**   |         10          |         22          |         34          |         46          |
|  МІ   |   **E**    |         11          |         23          |         35          |         47          |
|  ФА   |   **F**    |         12          |         24          |         36          |         48          |
|  ФА#  |   **F#**   |         13          |         25          |         37          |         49          |
| СОЛЬ  |   **G**    |         14          |         26          |         38          |         50          |
| СОЛЬ# |   **G#**   |         15          |         27          |         39          |         51          |
|  ЛЯ   |   **A**    |         16          |         28          |         40          |         52          |
|  ЛЯ#  |   **A#**   |         17          |         29          |         41          |         53          |
|  СІ   |   **B**    |         18          |         30          |         42          |         54          |
